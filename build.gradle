buildscript {
    ext {
        sonarqubeGradleVersion = '2.7.1'
    }
    dependencies {
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1")
    }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "jacoco"
apply plugin: "distribution"
apply plugin: "org.sonarqube"
apply plugin: 'project-report'
apply from: 'dependencies.gradle'

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'bftsmart'
version = '1.0.0-RC1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jacoco {
    toolVersion = "0.8.5"
}

test {
    dependsOn 'cleanTest'

    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat "full"
    }
}

sonarqube {
    properties {
        property 'sonar.java.coveragePlugin', "jacoco"
        property 'sonar.coverage.exclusions', "**/table/**, **/input/**, **/output/**, **/config/**"
        property 'sonar.exclusions', "file:**/generated-sources/**, **/table/**, **/input/**, **/output/**"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

plugins.withType(JacocoPlugin) {
  tasks["test"].finalizedBy 'jacocoTestReport'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    archivesBaseName='BFT-SMaRt-TPCC'
    project.version=project.version
}

distributions {
    main {
        contents {
            into('config') {
                from 'config'
            }
            into('data') {
                from 'data'
            }
            into('lib') {
                from jar
                from(configurations.runtimeClasspath)
            }
            from 'runscripts/smartrun.sh'
        }
    }
}